buildscript {
	ext {
		springBootVersion = '1.4.3.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath 'org.springframework:springloaded:1.2.0.RELEASE'
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'war'

jar {
	baseName = 'onepage'
	version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

configurations {
	providedRuntime
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-jdbc')
	compile('org.springframework.boot:spring-boot-starter-web')
	providedRuntime('org.springframework.boot:spring-boot-starter-tomcat')

	// mockito
	testCompile "org.mockito:mockito-all:1.10.19"
	testCompile "org.hamcrest:hamcrest-all:1.3"
	testCompile('org.springframework.boot:spring-boot-starter-test')

	// db
	compile 'mysql:mysql-connector-java:5.1.6'
	compile('commons-dbcp:commons-dbcp:1.2.2')

	compile "org.projectlombok:lombok:1.16.4"
	compile 'joda-time:joda-time:2.8.2'

	compile "com.mysema.querydsl:querydsl-jpa:3.7.2"
	compile "com.mysema.querydsl:querydsl-apt:3.7.2"

	testCompile('org.springframework.boot:spring-boot-starter-test')
}

sourceSets {
	generated {
		java {
			srcDirs = ['src/main/generated']
		}
	}
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
	source = sourceSets.main.java
	classpath = configurations.compile // + configurations.provided
	options.compilerArgs = [
			"-proc:only",
			"-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
	]
	destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
	dependsOn generateQueryDSL
	source generateQueryDSL.destinationDir
}

compileGeneratedJava {
	dependsOn generateQueryDSL
	options.warnings = false
//	Classpath += sourceSets.main.runtimeClasspath
}

clean {
	delete sourceSets.generated.java.srcDirs
}

idea {
	module {
		sourceDirs += file('src/main/generated')
	}
}

//eclipse {
//	Classpath {
//		containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
//		containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
//	}
//}

task wrapper(type: Wrapper) {
	gradleVersion = '2.7'
}
